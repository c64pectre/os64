;    test-iec-app: OS64 app test IEC.
;    Copyright (C) 2023  C64PECTRE
;
;    This program is free software: you can redistribute it and/or modify
;    it under the terms of the GNU General Public License as published by
;    the Free Software Foundation, either version 3 of the License, or
;    (at your option) any later version.
;
;    This program is distributed in the hope that it will be useful,
;    but WITHOUT ANY WARRANTY; without even the implied warranty of
;    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;    GNU General Public License for more details.
;
;    You should have received a copy of the GNU General Public License
;    along with this program.  If not, see https://www.gnu.org/licenses/.
;
;    Contact: https://github.com/c64pectre/os64/ create an issue

;region Constants

    BYTE_BIT_COUNT = 8

;endregion

;;; summary: Set clock line high (inverted)
;;; changed: A N Z
.proc clkhi
    lda CIA_2_PRA
    and # CIA_2_PRA_W_SERIAL_INV_CLOCK_OUT_CLR
    sta CIA_2_PRA
    rts
.endproc

;;; summary: Set clock line low  (inverted)
;;; changed: A N Z0
.proc clklo
    lda CIA_2_PRA
    ora # CIA_2_PRA_W_SERIAL_INV_CLOCK_OUT_SET
    sta CIA_2_PRA
    rts
.endproc

;;; summary: set data line high (inverted)
;;; changed: A N Z
.proc datahi
    lda CIA_2_PRA
    and # CIA_2_PRA_W_SERIAL_INV_DATA_OUT_CLR
    sta CIA_2_PRA
    rts
.endproc

;;; summary: Set data line low  (inverted)
;;; changed: A N Z0
.proc datalo
    lda CIA_2_PRA
    ora # CIA_2_PRA_W_SERIAL_INV_DATA_OUT_SET               ; $20
    sta CIA_2_PRA
    rts
.endproc

;;; summary: Debounce pia
;;; return:
;;;   C: data in
;;;   N: clock in
;;; changed: A+ N+ Z+ C+
.proc debpia
    from_loop_next
        lda CIA_2_PRA
        cmp CIA_2_PRA
        bne _loop
    end
    ;
    asl                                                     ;Shift data bit into C and clock into N
    ;
    rts
.endproc

;;; summary: Release ATN, delay and then release clock and data.
;;; changed: A+ N+ Z+
.proc dlabye
    jsr scatn                                               ;A+ N+ Z+
    jsr dladlh                                              ;A+ N+ Z+
    rts
.endproc

;;; summary: Delay approx 60 us end then set clock and data high
;;; changed: A+ N+ Z+
.proc dladlh
    txa
        from
            ldx # 10
        loop_next
            dex
            bne _loop
        end
    tax
    jsr clkhi                                               ;A+ N+ Z+
    jsr datahi                                              ;A+ N+ Z+
    rts
.endproc

;;; summary: Send byte at SELF_BSOUR
;;; return:
;;;   C  CC:OK/CS:FAIL
;;;   A  Error code on FAIL
;;; changed: A+ Y+ N+ V+ I0 Z+ C+
.proc isour
    try
        sei
        ;
        jsr datahi                                          ;A N Z  Make sure data is released
        jsr debpia                                          ;A+ N+ Z+ C+  Data should be low
        if_cs_then
            lda # KERNAL_IO_ERROR_DEVICE_NOT_PRESENT
            bcs _catch                                      ;bra
        else_end
        ;
        jsr clkhi                                           ;A N Z
        bit SELF_R2D2                                       ;N+ V+ Z+ EOI flag test
        if_mi_then
            ; Yes EOI: do the eoi
            ; wait for data to go high
            from_loop_next
                jsr debpia                                  ;A+ N+ Z+ C+
                bcc _loop
            end
            ; check P.C=1
            ;
            ;wait for data to go low
            from_loop_next
                jsr debpia                                  ;A+ N+ Z+ C+    
                bcs _loop
            end
            ; check P.C=0
        else_end
        ;
        ;wait for data high
        from_loop
            jsr debpia                                      ;A+ N+ Z+ C+
        next
            bcc _loop
        end
        ; check P.C=1
        ;
        jsr clklo                                           ;A+ N+ Z0
        ; check P.Z=0
        ;
        ; Send data
        from
            ;ldp_vb L_COUNT, BYTE_BIT_COUNT
            ldy # BYTE_BIT_COUNT
            ;sta L_COUNT
        loop
            jsr debpia                                      ;A+ N+ Z+ C+  Debounce bus and shift data bit into C and clock into N
            ; Data must be hi
            if_cc_then
                lda # KERNAL_IO_ERROR_FRAMING
                bne _catch                                  ;bra
            else_end
            ;
            ror SELF_BSOUR                                  ;next bit into carry
            if_cc_then
                jsr datalo                                  ;A+ N+ Z0
                ; check P.Z=0
                bne _end                ;bra
            else
                jsr datahi                                  ;A N Z
            end
            ;
            jsr clkhi                                       ;A+ N+ Z+
            nop
            nop
            nop
            nop
            lda CIA_2_PRA
            and # CIA_2_PRA_W_SERIAL_INV_DATA_OUT_CLR           ;data high ($FF-$20=$DF=%11011111)
            ora # CIA_2_PRA_W_SERIAL_INV_CLOCK_OUT_SET          ;clock low ($10=%00010000)
            sta CIA_2_PRA
        next
            ;dec_p L_COUNT
            ;dec L_COUNT
            dey
            bne _loop
        end

        lda # $04        ;set timer for 1ms
        sta CIA_1_TB_HI
        ; trigger one-shot timer
        lda # CIA_CRB_TIMER_B_START_STOP_START | CIA_CRB_TIMER_B_UNDERFLOW_RESTART_OFF | CIA_CRB_LOAD_LATCH_INTO_TIMER_ONCE_ON      ; (timrb=$19)
        sta CIA_1_CRB
        lda CIA_1_ICR                   ;clear the timer flags
        ;
        from_loop_next
            lda CIA_1_ICR
            and # CIA_ICR_TIMER_B_MASK      ; $02 = %00000010
            if_ne_then
                lda # KERNAL_IO_ERROR_FRAMING
                bne _catch              ; bra
            else_end
            ;
            jsr debpia                                      ;A+ N+ Z+ C+
            bcs _loop
        end
        ; check P.C=0
        cli
        bcc _finally                    ; bra
    catch
        ;jsr udst
        tay                                                 ; Save A (we use Y because it is already dirty, and tay/tya is cheaper that pha/pla)
            cli
            jsr dlabye
        tya                             
        sec
    finally_end
    ;
    rts
.endproc

;;; summary: isoura
;;; return:
;;;   C  CC:OK/CS:FAIL
;;; changed: A+ Y+ N+ V+ I0 Z+ C+
.proc isoura
    sei                                                     ;I1
    jsr clklo                                               ;A+ N+ Z0
    jsr datahi                                              ;A+ N+ Z+
    jsr w1ms                                                ;A+ N+ Z+
    jsr isour                                               ;A+ Y+ N+ V+ I0 Z+ C+
    ; ensure P.I = 0
    rts
.endproc

;;; summary: list1
;;; parameters:
;;;   A  Byte to send (firstly, any buffered byte is send)
;;; return:
;;;   C  CC:OK/CS:FAIL
;;;   A  Error code on fail
;;; changed: A+ Y+ N+ V+ I0 Z+ C+
.proc list1
    try
        pha
            bit SELF_C3P0                                   ;Character left in buf?
            if_mi_then
                ; yes
                ; Send buffered character
                sec                                         ;Set eoi flag
                ror SELF_R2D2
                ;
                jsr isour                                   ;A+ Y+ N+ V+ I0 Z+ C+  Send last character
                ;bcs _catch                                 ;is not in Kernal
                ;
                lsr SELF_C3P0                               ;buffer clear flag
                lsr SELF_R2D2                               ;clear eoi flag
            else_end
            ;
            ;lda_p A_PA                                     ;talk/listen address
        pla
        sta SELF_BSOUR
        ;
        sei                                                 ;below isoura -> isour does cli
        ;
        jsr datahi                                          ;A+ N+ Z+
        cmp # SERIAL_COMMAND_UNLISTEN                       ;clkhi only on unlisten
        if_eq_then
            jsr clkhi                                       ;A+ N+ Z+
        else_end
        ;
        lda CIA_2_PRA                                       ;assert attention
        ora # CIA_2_PRA_W_SERIAL_INV_ATN_OUT_SET
        sta CIA_2_PRA
        ;
        jsr isoura                                          ;A+ Y+ N+ V+ I0 Z+ C+
        ;bcs _catch
        ;bcc _finally
    catch_finally_end
    ; check I=0
    ; CC:OK/CS:FAIL
    rts
.endproc

;;; summary: Disable rs232. Need to enable in user code.
;;; changed: A+ Z0 N0
.proc rsp232
    ;pha                                                     ;TODO kan weg
        lda # CIA_ICR_FLAG_DISABLE
        sta CIA_2_ICR
    ;pla                                                     ;TODO kan weg
    ;
    rts
.endproc

;;; summary: Release attention
;;; changed: A+ N+ Z+
.proc scatn
    lda CIA_2_PRA
    and # CIA_2_PRA_W_SERIAL_INV_ATN_OUT_CLR
    sta CIA_2_PRA
    rts
.endproc

;;; summary: IEC talk or listen
;;; parameters:
;;;   A     IEC command to ora A_PA with
;;;   A_PA  Primary (file/device) address
;;; returns:
;;;   C  CC:OK/CS:FAIL
;;;   A  Error code on fail
;;; changed: A+ Y+ N+ V+ I0 Z+ C+
.proc talk_or_listen
    ;[FP  ] <== X = [FP] = [RSP]
    ;[FP-1] A_PA
    A_PA = < -1
    ;
    ora_p A_PA
    st_a_p A_PA
    ;
    jsr rsp232                                              ;A+ Z0 N0  Protect self from rs232 nmi's
    ld_a_p A_PA
    jsr list1                                               ;CC:OK/CS:FAIL  A+ Y+ N+ V+ I0 Z+ C+
    rts
.endproc

;;; summary: IEC untalk or unlisten
;;; return:
;;;   C  CC:OK/CS:FAIL
;;; changed: A+ Y+ N+ V+ I0 Z+ C+
.proc untalk_or_unlisten
    jsr list1                                               ;CC:OK/CS:FAIL  A+ Y+ N+ V+ I0 Z+ C+  Send it.
    tay                                                     ;A+ N+ Z+  Save error code, if any
        jsr dlabye                                          ;A+ N+ Z+
    tya                                                     ;A+ N+ Z+  Restore error code, if any
    rts
.endproc

;;; summary: Delay 1ms using loop
;;; changed: A+ N+ Z+
.proc w1ms
    txa
        from
            ldx # 200-16                ;1000us-(1000/500*8=#40us holds)
        loop_next
            dex                         ;5us loop
            bne _loop
        end
    tax
    rts
.endproc
